# Домашнее задание по теме "Приёмы программирования, интеграция с внешними сервисами" 

## Цель домашнего задания

Реализовать автоматический расчет суммы и управление видимостью элементов в документе Покупка.
Реализовать загрузку курсов валют.

## Задание

1. В качестве основы используйте базу, полученную в результате выполнения домашнего задания к занятию 2

2. Создайте форму документа покупка и реализуйте:
      - расчет стоимости
      - суммы документа
      - отображение списка товаров в зависимости от настройки формы.

3. Создайте регистр сведений для хранения курсов валют.

4. Создайте обработку для загрузки курсов валют.



## Процесс выполнения

### Создание и настройка формы документа

1. Откройте в конфигураторе информационную базу, полученную в результате выполнения домашнего задания к занятию 2

2. Создайте форму документа для документа Покупка

3. Реализуйте в форме документа процедуру для управления видимостью элементов **УправлениеВидимостьюЭлементовФормы** с директивой компиляции **&НаКлиентеНаСервереБезКонтекста**.

<details>
      <summary>Примерный код процедуры</summary>

```bsl
&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостьюЭлементовФормы(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	РазделитьПоТоварам = Объект.РазделитьПоТоварам;
	
	Элементы.ТоварыУслуги.Видимость = РазделитьПоТоварам;
	Элементы.Сумма.ТолькоПросмотр = РазделитьПоТоварам;
	
КонецПроцедуры
```

</details>

4. Для формы назначте обработчик события **ПриСозданииНаСервере**. В обработчике вызовите ранее созданную процедуру `УправлениеВидимостьюЭлементовФормы(ЭтотОбъект);`

5. Для элемента **РазделитьПоТоварам** назначте обработчик события **ПриИзменении**. В обработчике вызовите ранее созданную процедуру `УправлениеВидимостьюЭлементовФормы(ЭтотОбъект);`

6. Реализуйте в документе процедуру для расчета суммы документа **РассчитатьИтогДокумента** с директивой компиляции **&НаКлиенте**.

<details>
      <summary>Примерный код процедуры</summary>

```bsl
&НаКлиенте
Процедура РассчитатьИтогДокумента()
	
	Объект.Сумма = Объект.ТоварыУслуги.Итог("Сумма");
	
КонецПроцедуры
```

</details>

7. Реализуйте в документе процедуру для расчета стоимость в строке табличной части и последующего пересчета суммы документа **РассчитатьСуммуСтроки** с директивой компиляции **&НаКлиенте**.

<details>
      <summary>Примерный код процедуры</summary>

```bsl
&НаКлиенте
Процедура РассчитатьСуммуСтроки(Строка)
	
	Строка.Сумма = Строка.Цена * Строка.Количество;
	
	РассчитатьИтогДокумента();
	
КонецПроцедуры                         
```

</details>

8. Для элементов **НоменклатураКоличество** и **НоменклатураЦена** назначте обработчики события **ПриИзменении**. В обработчике вызовите ранее созданную процедуру с передачей текущей строки:

```bsl
	ТекущиеДанные = Элементы.ТоварыУслуги.ТекущиеДанные;
	РассчитатьСуммуСтроки(ТекущиеДанные);
```

### Создание регистра сведений для хранения курсов валют

1. Создайте регистр сведений **КурсыВалют**. Периодичность - День.
      - Измерение **Валюта** - СправочникСсылка.Валюты
      - Ресурс **Кратность** - Число (10, 0)
      - Ресурс **Курс** - Число (15, 4)

2. Включите регистр сведений в одну из созданных подсистем

### Загрузка курсов валют

1. Создайне обработку **ЗагрузкаКурсаВалюты**

2. Включите обработку в одну из созданных подсистем

3. Создайте форму обработки

4. Создайте команду на форме обработки, вынесите на командную панель, назначьте кнопкой по умолчанию, создайте обработчик на клиенте и на сервере

5. В процедуру на клиенте после вызова сервера добавьте строчку для оповещения пользователя об успешной загрузке `ПоказатьОповещениеПользователя("Курсы валют загружены");`

6. В процедуру на сервере добавьте последовательный вызов процедур по получению данных с сайта и записи полученных данных в базу:

```bsl
	ДанныеСайта = ПолучитьДанныеССайта();
	ЗаписатьКурсыВалют(ДанныеСайта);
```

7. Реализуйте алгоритм процедуры по получению данных с сайта и записи полученных данных в базу:

<details>
      <summary>Пример кода для решения задачи</summary>

```bsl
&НаСервере
Функция ПолучитьДанныеССайта()
	
	Соединение = Новый HTTPСоединение("www.cbr-xml-daily.ru", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	
	Запрос = Новый HTTPЗапрос("/daily_utf8.xml");
	
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение "Ошибка соединения с сайтом ЦБ";
	КонецЕсли;
	
	Возврат Ответ.ПолучитьТелоКакСтроку();
	
КонецФункции

&НаСервере
Процедура ЗаписатьКурсыВалют(ДанныеСайта)
	
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(ДанныеСайта);
	
	Построитель = Новый ПостроительDOM;
	Документ = Построитель.Прочитать(Чтение);
	
	Для Каждого Узел Из Документ.ЭлементДокумента.ДочерниеУзлы Цикл
		
		Кратность = 1;
		Курс = 1;
		СимвольныйКод = "";
		
		Для Каждого СвойствоВалюты Из Узел.ДочерниеУзлы Цикл
			Если СвойствоВалюты.ИмяУзла = "CharCode" Тогда
				СимвольныйКод = СвойствоВалюты.ТекстовоеСодержимое;
			КонецЕсли;
			Если СвойствоВалюты.ИмяУзла = "Nominal" Тогда
				Кратность = Число(СвойствоВалюты.ТекстовоеСодержимое);
			КонецЕсли;
			Если СвойствоВалюты.ИмяУзла = "Value" Тогда
				Курс = Число(СвойствоВалюты.ТекстовоеСодержимое);
			КонецЕсли;
		КонецЦикла;
		
		Валюта = Справочники.Валюты.НайтиПоКоду(СимвольныйКод);
		
		Если Не ЗначениеЗаполнено(Валюта) Тогда
			Продолжить;
		КонецЕсли;		
		
		Запись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
		Запись.Период = ТекущаяДата();
		Запись.Валюта = Валюта;
		Запись.Кратность = Кратность;
		Запись.Курс = Курс;
		Запись.Записать();
		
	КонецЦикла;	
	
КонецПроцедуры
```

</details>

### Критерии оценки
Данное домашнее задание предназначено для самостоятельной практики.
Проверка экспертом не предусмотрена.

Успешная реализация автоматического расчета суммы и управление видимостью элементов в документе Покупка и будет критерием оценки.
Вы сможете сравнить свою работу с эталонным домашним заданием, которое получите по ссылке в личном кабинете студента в ответ на отправку своего варианта работы.
