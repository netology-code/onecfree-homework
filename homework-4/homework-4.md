# Домашнее задание по теме "Использование накопленных данных. Отчёты и дашборды"

## Цель задания:

Реализовать алгоритмы проведения документов.
Реализовать отчеты и дашборды по накопленным данным.

## Задание

1. В качестве основы используйте базу, полученную в результате выполнения домашнего задания к занятию 3

2. Реализуйте метод для перевода суммы из рублей в валюту и обратно

3. Создайте регистры накопления для хранения данных и реализуйте алгоритм формирования движений документов

4. Создайте отчет по движениям денежных средств

5. Создайте дашбор для отображения диаграм по состоянию текущих финансов, разместите дашборд на начальной странице



## Процесс выполнения

Перед началом выполнения задания желательно добавить 5-10 документов каждого вида и загрузить курсы валют, чтобы отчеты в финальной части задания содержали достаточное количество данных.

### Реализация метода для перевода сумм из рублей в валюту и обратно

1. Реализуйте экспортные методы **СуммаВВалюте** и **СуммаВРублях** в модуле менеджера регистра сведений **КурсыВалют**

<details>
      <summary>Примерный код процедур</summary>

```bsl
Функция СуммаВВалюте(Сумма, Валюта, Период = Неопределено) Экспорт
	
	Если Валюта = Справочники.Валюты.РоссийскийРубль Тогда
		Возврат Сумма;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КурсыВалютСрезПоследних.Кратность КАК Кратность,
	               |	КурсыВалютСрезПоследних.Курс КАК Курс
	               |ИЗ
	               |	РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &Валюта) КАК КурсыВалютСрезПоследних";
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Валюта", Валюта);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ШаблонСообщения = "Курс валюты %1 не установлен";
		ВызватьИсключение СтрШаблон(ШаблонСообщения, Валюта);
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Сумма * Выборка.Кратность / Выборка.Курс;	
	
КонецФункции

Функция СуммаВРублях(Сумма, Валюта, Период = Неопределено) Экспорт
	
	Если Валюта = Справочники.Валюты.РоссийскийРубль Тогда
		Возврат Сумма;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КурсыВалютСрезПоследних.Кратность КАК Кратность,
	               |	КурсыВалютСрезПоследних.Курс КАК Курс
	               |ИЗ
	               |	РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &Валюта) КАК КурсыВалютСрезПоследних";
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Валюта", Валюта);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ШаблонСообщения = "Курс валюты %1 не установлен";
		ВызватьИсключение СтрШаблон(ШаблонСообщения, Валюта);
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Сумма * Выборка.Курс / Выборка.Кратность;	
	
	
КонецФункции
```

</details>

### Создание регистров накопления

1. Создайте регистр накопления **ЛичныеСчета** с видом Остатки. Регистраторы: ПолучениеДохода, Покупка, ПереводМеждуСчетами
      - Измерение **Счет** - СправочникСсылка.ЛичныеСчета
      - Измерение **Валюта** - СправочникСсылка.Валюты
      - Ресурс **Сумма** - Число (15, 2)
      - Ресурс **СуммаВал** - Число (15, 2)

2. Создайте регистр накопления **Доходы** с видом Оброты. Регистраторы: ПолучениеДохода
      - Измерение **СтатьяДохода** - СправочникСсылка.СтатьиДвиженийДенежныхСредств
      - Измерение **Валюта** - СправочникСсылка.Валюты
      - Ресурс **Сумма** - Число (15, 2)
      - Ресурс **СуммаВал** - Число (15, 2)

3. Создайте регистр накопления **Расходы** с видом Оброты. Регистраторы: ПолучениеДохода
      - Измерение **СтатьяДохода** - СправочникСсылка.СтатьиДвиженийДенежныхСредств
      - Измерение **Номенклатура** - СправочникСсылка.Номенклатура
      - Измерение **Валюта** - СправочникСсылка.Валюты
      - Ресурс **Сумма** - Число (15, 2)
      - Ресурс **СуммаВал** - Число (15, 2)

### Формирование движений

1. Для документа **ПолучениеДохода** с помощью конструктора движений создайте алгоритм формирования движения по регистрам **ЛичныеСчета** и **Доходы**

2. Для документа **Покупка** с помощью конструктора движений создайте алгоритм формирования движения по регистрам **ЛичныеСчета** и **Расходы**

3. Для документа **ПереводМеждуСчетами** с помощью конструктора движений создайте алгоритм формирования движения по регистру **ЛичныеСчета**

4. Во всех документах для заполнения ресурсов Сумма и СуммаВал используйте методы СуммаВВалюте и СуммаВРублях из модуля менеджера регистра **КурсыВалют**

5. Перепроведите все документы, имеющиеся в системе, убедитесь, что в регистрах накопления появились движения.

### Отчет по движениям денежных средств

1. Создайте новый отчет **ДвиженияДенежныхСредств**

2. Создайте в отчете основную схему компоновки данных

3. На закладке Наборы данных добавьте новый набор данных с типом Запрос

4. С помощью конструктора запроса сформируйте текст запроса к виртуальной таблице регистра накопления **ЛичныеСчета.ОстаткиИОбороты**, выберите все поля, в параметнах виртуальной таблицы укажите Периодичность - Запись

5. На закладке ресурсы добавьте в список ресурсов **СуммаОборот**, **СуммаНачальныйОстаток**, **СуммаКонечныйОстаток**

6. На закладке Настройки добавьте группироку по Счету и внутрь ее группировку Детальные записи

7. В выбранных полях выберите **Период**, **СуммаНачальныйОстаток**, **СуммаОборот** и **СуммаКонечныйОстаток**

8. Сформируйте отчет, попробуйте изменить внешний вид и структуру отчета на свое усмотрение

### Дашборд

1. Создайте новую общую форму **НачальнаяСтраница**

2. В свойстве корня конфигурации **Рабочая область начальной страницы** добавьте в состав форму **НачальнаяСтраница**

3. Добавьте в реквизиты формы **НачальнаяСтраница** реквизиты с типом Диаграмма: **ДоходыПоМесяцам**, **ОстаткиНаСчетах**, **РасходыПоМесяцам**, **ТопТоваров**. Выберите для диаграмм подходящий тип.

4. Перенестие диаграммы на форму и расположите по группа на свое усмотрение.

5. Реализуйте методы для заполнения диграмм

<details>
      <summary>Примерный код процедур</summary>

```bsl
&НаСервере
Процедура ОбновитьДанныеДиаграмм() 
	
	ОбновитьОстаткиНаСчетах();
	ОбновитьТопРасходов();
	ОбновитьДоходыЗаМесяц();
	ОбновитьРасходыЗаМесяц();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОстаткиНаСчетах()  
	
	Диаграмма = ОстаткиНаСчетах;
	
	Диаграмма.Очистить();	
	Диаграмма.Обновление = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЛичныеСчетаОстатки.Счет КАК Счет,
	               |	ЛичныеСчетаОстатки.СуммаОстаток КАК СуммаОстаток
	               |ИЗ
	               |	РегистрНакопления.ЛичныеСчета.Остатки КАК ЛичныеСчетаОстатки";
	
	Точка = Диаграмма.Точки.Добавить("Остаток на счете (руб.)");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Серия = Диаграмма.УстановитьСерию(Выборка.Счет);
		Диаграмма.УстановитьЗначение(Точка, Серия, Выборка.СуммаОстаток);
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТопРасходов()
	
	Диаграмма = ТопТоваров;
	
	Диаграмма.Очистить();	
	Диаграмма.Обновление = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 5
	               |	РасходыОбороты.ТоварУслуга КАК ТоварУслуга,
	               |	РасходыОбороты.СуммаОборот КАК СуммаОборот
	               |ИЗ
	               |	РегистрНакопления.Расходы.Обороты(&НачалоПериода, &КонецПериода, , ) КАК РасходыОбороты
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	СуммаОборот УБЫВ";
	
	Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(НачалоДня(ТекущаяДата()), -1));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ТекущаяДата()));
	
	Точка = Диаграмма.Точки.Добавить("Расходы на товар (руб.)");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Серия = Диаграмма.УстановитьСерию(Выборка.ТоварУслуга);
		Диаграмма.УстановитьЗначение(Точка, Серия, Выборка.СуммаОборот);
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоходыЗаМесяц()
	
	Диаграмма = ДоходыПоМесяцам;
	
	Диаграмма.Очистить();	
	Диаграмма.Обновление = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоходыОбороты.СтатьяДохода КАК СтатьяДохода,
	               |	ДоходыОбороты.СуммаОборот КАК СуммаОборот,
	               |	ДоходыОбороты.Период КАК Период
	               |ИЗ
	               |	РегистрНакопления.Доходы.Обороты(&НачалоПериода, &КонецПериода, Месяц, ) КАК ДоходыОбороты";
	
	Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(НачалоДня(ТекущаяДата()), -6));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ТекущаяДата()));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Точка = Диаграмма.УстановитьТочку(Формат(Выборка.Период, "ДФ='ММММ гггг'"));
		Серия = Диаграмма.УстановитьСерию(Выборка.СтатьяДохода);
		Диаграмма.УстановитьЗначение(Точка, Серия, Выборка.СуммаОборот);
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРасходыЗаМесяц()
	
	Диаграмма = РасходыПоМесяцам;
	
	Диаграмма.Очистить();	
	Диаграмма.Обновление = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РасходыОбороты.СуммаОборот КАК СуммаОборот,
	               |	РасходыОбороты.Период КАК Период,
	               |	РасходыОбороты.СтатьяРасхода КАК СтатьяРасхода
	               |ИЗ
	               |	РегистрНакопления.Расходы.Обороты(&НачалоПериода, &КонецПериода, Месяц, ) КАК РасходыОбороты";
	
	Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(НачалоДня(ТекущаяДата()), -6));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ТекущаяДата()));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Точка = Диаграмма.УстановитьТочку(Формат(Выборка.Период, "ДФ='ММММ гггг'"));
		Серия = Диаграмма.УстановитьСерию(Выборка.СтатьяРасхода);
		Диаграмма.УстановитьЗначение(Точка, Серия, Выборка.СуммаОборот);
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;	
	
КонецПроцедуры
```

</details>

6. Назначте обработчик на событие формы **ПриСозданииНаСервере** и вызовите внутри обработчика метод `ОбновитьДанныеДиаграмм`

7. Создайте команду **Обновить**, вынесите на форму, создайте обработчик и вызовите внутри метод `ОбновитьДанныеДиаграмм`

8. Запустите информационную базу в пользовательском режиме и убедитесь, что диаграммы формируются

### Критерии оценки
Данное домашнее задание предназначено для самостоятельной практики.
Проверка экспертом не предусмотрена.

Успешная реализация алгоритмов проведения документов,  отчетов и дашбордов по накопленным данным и будет критерием оценки.
Вы сможете сравнить свою работу с эталонным домашним заданием, которое получите по ссылке в личном кабинете студента в ответ на отправку своего варианта работы.
